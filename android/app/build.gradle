

plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

// ابدأ من هنا: إضافة الكود لتحميل ملف key.properties
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.withInputStream {
        keystoreProperties.load(it)
    }
}
// انتهى جزء إضافة الكود لتحميل ملف key.properties


android {
    namespace = "io.github.abdulrasol.store_mangment"
    compileSdk = flutter.compileSdkVersion
    ndkVersion "26.1.10909125"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    defaultConfig {
        applicationId = "io.github.abdulrasol.store_mangment"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        ndk {
            abiFilters "arm64-v8a"
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                def keyPath = keystoreProperties['storeFile']
                def keyFile = new File(keyPath)
                if (!keyFile.isAbsolute()) {
                    keyFile = new File(keystorePropertiesFile.parentFile, keyPath)
                }
                storeFile keyFile
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                println "Using release signing config from key.properties"
            } else {
                println "key.properties not found, using default debug signing"
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

flutter {
    source = "../.."
}

configurations.all {
    resolutionStrategy {
        force 'androidx.browser:browser:1.8.0'
        force 'androidx.activity:activity-ktx:1.9.3'
        force 'androidx.activity:activity:1.9.3'
        force 'androidx.core:core-ktx:1.13.1'
        force 'androidx.core:core:1.13.1'
    }
}